store-procedure next-cur-identifier
    ~force search-forward &cat '\<' &cat $identifier '\>'
~endm

store-procedure next-alt-identifier
    ~local %osearch
    set-variable %osearch $identifier
    alternate-buffer
    ~force search-forward &cat '\<' &cat %osearch '\>'
    position-window "+"
    alternate-buffer
~endm

store-procedure man-cur-identifier
    ~force 1 shell-command &cat "man " $identifier
~endm

store-procedure write-and-ex
    ~local %tmp
    set-variable %tmp &cat $cfilname ".extmp"
    ~if &writable %tmp
        beep
        write-message &cat %tmp " already exists; not running ex."
    ~else
        1 goto-line
        write-til goto-line %tmp
        1 shell-command &cat "ex " %tmp
        filter-til goto-line &cat "cat " %tmp
        1 shell-command &cat "rm " %tmp
    ~endif
~endm

store-procedure write-and-aspell
    write-file
    1 shell-command &cat "aspell check " $cfilname
    replace-with-file $cfilname
~endm

store-procedure filter-cur-graf-par
    setv %cl $curline
    setv %cc $curcol
    next-line
    previous-paragraph
    filter-til next-paragraph "par w72h1s0"
    %cl goto-line
    %cc goto-column
~endm

store-procedure filter-par-til
    goto-named-mark '<
    filter-til goto-named-mark '> "par w72h1s0"
~endm
